. = torg + 1000

main:
    mov pc, sp
    tst -(sp)

    mov #root, -(sp)
    tst -(sp)
    jsr pc, treeMax
    mov (sp)+, r1       ;r1 <- max
    tst (sp)+
    halt


;Gets a tree node and returns the max integer stored in the subtree.
;input stack = ret-adrs -> max (output) -> nodeAdrs
;usage stack = sonMax -> sonAdrs -> tempMax -> retAdrs -> max (output) -> nodeAdrs
treeMax:
    tst 4(sp)
    bne notEmpty        ;if nodeAddress=NIL then return 0.
        mov #0, 2(sp)
        rts pc
notEmpty:
    mov @4(sp), -(sp)   ;push: stack= tempMax, retAdrs, output, nodeAdrs
    mov 6(sp), r0
    mov 2(r0), -(sp)    ;push: stack= lsAdrs, tempMax, retAdrs, output, nodeAdrs
    tst -(sp)           ;push: stack= <lsMax>, lsAdrs, tempMax, retAdrs, output, nodeAdrs
    jsr pc, treeMax     ;treeMax(retValue, leftSonAddress)
    cmp (sp), 4(sp)
    ble lsNotMax        ;if (lsMax > tempMax)
        mov (sp), 4(sp) ;   tempMax <- lsMax
lsNotMax:
    mov 12(sp), r0
    mov 4(r0), 2(sp)    ;edit stack: lsAdrs <- rsAdrs
    jsr pc, treeMax     ;treeMax(retValue, rightSonAddress)
    cmp (sp), 4(sp)
    ble rsNotMax        ;if (rsMax > tempMax)
        mov (sp), 4(sp) ;   tempMax <- rsMax
rsNotMax:
    tst (sp)+           ;pop sonMax
    tst (sp)+           ;pop: stack= tempMax, retAdrs, output, nodeAdrs
    mov (sp)+, 2(sp)    ;pop tempMax -> output
    rts pc


. = torg + 5000
;______4_______
; _2_     _8_
; 1 9    7   5
;   6    6

root:   .word 4., n01, n05
n01:    .word 2., n02, n03
n02:    .word 1., 0, 0
n03:    .word 9., n04, 0
n04:    .word 6., 0, 0
n05:    .word 8., n06, n08
n06:    .word 7., n07, 0
n07:    .word 6., 0, 0
n08:    .word 5., 0, 0
